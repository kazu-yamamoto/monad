# リスト

<div class="right">
「見方によっては関数プログラミングはリスト処理と同義である」<br /> Jeremy Gibbons<br /> 関数プログラミングの楽しみ<br />

</div>

この章では、リストに関する基本的な関数 map、(++)、concat、および concatMap を復習します。なぜこれらの関数を復習するかというと、map と concatMap は Monad を理解するために重要であり、(++) と concat は concatMap を定義するために必要だからです。

## 写像 (map)
再帰を使えば、リストを操作する関数はなんでも書けます。しかし再帰は用途が広いので、再帰を使ったコードは意図が不明瞭になりがちです。

意図を明確にするには、再帰よりも用途が限定された関数を使うべきです。畳み込み関数 foldr は、再帰よりも用途が限定された関数です。map や filter の用途は、foldr よりもさらに限定されています。

そこでリストを扱うときは、まず最初に map や filter が利用できないか考え、ダメなら foldr の使用を検討し、最終手段として再帰を用いるのがよい習慣です。

map は、第一引数である関数を第二引数であるリストの中身に対して適用する関数です。以下に利用例を示します。

    > map (+1) [1..5]
    [2,3,4,5,6]
文献(Haskell98)では、map 自体は再帰を使って定義しています。

    map :: (a -> b) -> [a] -> [b]
    map _ []     = []
    map f (x:xs) = f x : map f xs
ただし、map はfoldr を使っても定義可能です。(foldr は、再帰を使って定義されています。)

    map :: (a -> b) -> [a] -> [b]
    map f = foldr (\x xs -> f x : xs) []
ここで、関数 f :: a -> b を map の第一引数に部分適用してみましょう。

    f :: a -> b
    map f :: [a] -> [b]
型をよく見比べて下さい。型 a から型 b を返す関数が、[a] から [b] を返す関数へ変換されたことが分かります。このことを「map は関数 f をリストの世界に持ち上げる(lift)」と表現します。慣習として、通常の型よりも、リストの世界の方が上にあると考えるからです。

写像はリストに限った手法ではなく、さまざまなデータ型にも適用できます。そこで、写像関数 API とする型クラスが定義されています。その型クラスの名前を Functor といいます。

Functor が提供する写像関数の名前は、fmap といいます。

    class Functor m where
        fmap :: (a -> b) -> f a -> f b
もちろんリストは Functor のインスタンスです。リストは Functor のインスタンスであり、その実装の一例は以下のようになります。

    instance Functor [] where
        fmap = map
fmap は、二項演算子として使うとコードの見通しがよくなります。ここでは、上記の簡単な例を書き換えてみましょう。

    > (+1) `fmap` [1..5]
    [2,3,4,5,6]
Control.Applicative モジュールでは、fmap の別名として (<$>) を定義していますので、さらに読みやすくなります。

    > :m Control.Applicative
    > (+1) <$> [1..5]
    [2,3,4,5,6]
(<$>)が、関数適用の二項演算子($)と似ているところがポイントです。(<$>)は、特殊な関数適用なのです。これ以降、本書では fmap の代わりに (<$>) を使います。

map には、リストの大きさを変えないという性質があります。一般化された (<$>) も、その性質を持っています。すなわち、Functor を「物を入れる箱」と見なせば、(<$>)は箱の種類や大きさは変えず、中身の値だけを変化させます。

この性質は、以下のように表現されます。

    f <$> g <$> m = (f . g) <$> m
(<$>) に慣れていない内は、箱をリストに限定した map で考える方が分かりやすいかもしれません。

    map f (map g m) = map (f . g) m
例を挙げてみましょう。文字列中のアルファベットを次のアルファベットに変換してみます。これには、文字を ord で一旦数値に直し、1 を加えた後、文字 chr で文字に戻すことになります。

    > :m Data.Char
    > map chr $ map (+1) $ map ord ['a'..'c']
    → "bcd"
上記の法則が言っているのは、これら三つの map を一つにまとめられるということです。

    > :m Data.Char
    > map (chr . (+1) . ord) ['a'..'c']
    → "bcd"
Haskell のコンパイラーが素朴な実装であれば、map を一つにまとめた方がプログラムが速くなります。なぜなら、中間のリストを生成しないからです(図xxx)。この手法を融合(fusion)と言います。

hlint というプログラムを使えば、融合変換を施せる場所を指摘してくれます。なお GHC では、融合変換を自動的にやってくれます。

融合変換に慣れたところで、(<$>) を使って最後の例の見た目を変えてみましょう。

    > :m Control.Applicative Data.Char
    > chr . (+1) . ord <$> ['a'..'c']
    → "bcd"
(<$>) よりも(.)の方が結合力が強いので、括弧が不要になります。

## 追加 (++)
(++) は、第一式引数を第二引数へ追加する関数です。

    > "Hello, " ++ "World!"
    → "Hello, World!"
文献(Haskell98)では、(++)を再帰を用いて定義しています。

    (++) :: [a] -> [a] -> [a]
    []     ++ ys = ys
    (x:xs) ++ ys = x : (xs ++ ys)
リストの性質を活かして、コピーを取るのは第一引数のみであり、第二引数は共有されるのが分かります(図xxx)。

(++) は foldr を用いて、以下のように定義することもできます。

    (++) :: [a] -> [a] -> [a]
    xs ++ ys = foldr (:) ys xs
foldr に慣れている人にとっては、こちらの定義の方がよいでしょう。なぜなら、(++) は右から畳み込んだ方が効率がよいと主張しているのが分かるからです。

第xxx章で、MonadPlus という型クラスの API として、mplus が出てきます。実は、リストの mplus は (++) そのものです。

(xxx 疑問 ++ は再帰で、concat は foldr。なぜ？)

## 連結 (concat)
concat は、リストのリストを取り、リストを返す関数です。以下に使い方の例を示します。

    > concat [[1],[2,3],[4,5,6]]
    → [1,2,3,4,5,6]
concat を再帰を用いて定義すると以下のようになります。

    concat :: [[a]] -> [a]
    concat [] = []
    concat (xs:xss) = xs ++ concat xss
なお、文献(Haskell98)では、concat を foldr を用いて以下のように定義しています。

    concat :: [[a]] -> [a]
    concat = foldr (++) []
## 写像して連結 (concatMap)
concatMap は、concat と map を合成した関数です。

    concatMap   :: (a -> [b]) -> [a] -> [b]
    concatMap f =  concat . map f
concatMap 動作を段階を踏んで理解しましょう。まず、次のような数値のリストのリストに filter even を map してみます。

    > map (filter even) [[1],[2,3],[4,5,6]]
    [[],[2],[4,6]]
偶数のリストのリストできました。concat を使うと、内側のリストを潰すことができます。

    > concat $ map (filter even) [[1],[2,3],[4,5,6]]
    [2,4,6]
これを同時にやるのが、concatMap です。

    > concatMap (filter even) [[1],[2,3],[4,5,6]]
    [2,4,6]
この例では、concatMap の嬉しさは伝わらないでしょう。concatMap が活躍するのは、リスト内包表記やリストを Monad として使ったときです。実は、concatMap の第一式数と第二引数を入れ替えると、リストの (>>=) になります。リストの (>>=) を定義する例を以下に示します。

    listBind :: [a] -> (a -> [b]) -> [b]
    listBind = flip concatMap
    
    instance Monad [] where
        (>>=) = listBind
型が分かるように、まず listBind という関数を定義して、それをリストの (>>=) に指定しています。なお、文献(Haskell98) では、以下のように定義されています。

    instance Monad [] where
        f >>= m = concat (map f m)
リストの (>>=) がどういう役割を果たすのかは、第xxx章で明らかにします。


----------------------------------------------------------------

著作権は山本和彦に属します。意見、質問、議論などは、[github の Issues](http://github.com/kazu-yamamoto/monad/issues)へ。

<style type="text/css">
<!--
.right {
	text-align: right;
	padding-right: 32px;
	padding-bottom: 32px;
}
-->
